buildscript {
  repositories {
    mavenCentral()
    maven {
      url "https://artifacts.fizzpod.com/public/mvn/"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath ('com.fizzpod:gradle-plugin-opinion:13.1.0') {
      exclude group: 'com.fizzpod', module: 'gradle-extended-info-plugin'
    }
  }
}

apply plugin: 'com.fizzpod.pater-build'
apply from: 'buildSrc/build.gradle'

sourceSets {
  main {
    java {
      srcDir 'buildSrc/src/main/java'
    }
    resources {
      srcDir 'buildSrc/src/main/resources'
    }
    groovy {
      srcDir 'buildSrc/src/main/groovy'
    }
  }
  test {
    java {
      srcDir 'buildSrc/src/test/java'
    }
    resources {
      srcDir 'buildSrc/src/test/resources'
    }
    groovy {
      srcDir 'buildSrc/src/test/groovy'
    }
  }
}

dependencies {
  api gradleApi()
  api localGroovy()
  constraints {
        implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10') {
            because 'version 1.3.5 as defined by nebula gradle-info-plugin has a known vulnerability'
        }
        implementation('org.bouncycastle:bcpg-jdk15on') {
            because 'version 1.64 as defined by nebula gradle-info-plugin has a known vulnerability'
            version {
                require '1.70'
            }
        }
        implementation('org.eclipse.jgit:org.eclipse.jgit') {
            because 'version 1.64 as defined by nebula gradle-info-plugin has a known vulnerability'
            version {
                require '5.13.0.202109080827-r'
            }
        }
        implementation ('com.jcraft:jsch.agentproxy.connector-factory') {
          because 'version 0.0.7 as defined by nebula gradle-info-plugin via svnkit has a known vulnerability'
            version {
                require '0.0.9'
            }
        }
    }
}

dependencyCheck {
  suppressionFile="./cveSuppressionFile.xml"
}

verifyRelease.dependsOn dependencyCheckAnalyze

pluginBundle {

  description = 'Gradle plugin extending the nebula-info-plugin providing extension points and support for other CI servers.'
  tags = ['ci', 'info', 'build']

  plugins {

    plugin {
      id = 'com.fizzpod.info'
      displayName = 'Gradle Extended Info Plugin'
    }
  }
}

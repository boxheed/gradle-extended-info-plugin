buildscript {
  repositories {
    mavenCentral()
    maven {
      url "https://artifacts.fizzpod.com/public/mvn/"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath ('com.fizzpod:gradle-plugin-opinion:16.1.4') {
      exclude group: 'com.fizzpod', module: 'gradle-extended-info-plugin'
    }
  }
}

apply plugin: 'com.fizzpod.pater-build'
apply from: 'buildSrc/build.gradle'

sourceSets {
  main {
    java {
      srcDir 'buildSrc/src/main/java'
    }
    resources {
      srcDir 'buildSrc/src/main/resources'
      exclude 'META-INF/gradle-plugins/com.fizzpod.info.properties'
    }
    groovy {
      srcDir 'buildSrc/src/main/groovy'
    }
  }
  test {
    java {
      srcDir 'buildSrc/src/test/java'
    }
    resources {
      srcDir 'buildSrc/src/test/resources'
    }
    groovy {
      srcDir 'buildSrc/src/test/groovy'
    }
  }
}

dependencies {
  api gradleApi()
  api localGroovy()
  constraints {
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8') {
      because 'version 1.3.5 as defined by nebula gradle-info-plugin has a known vulnerability'
      version {
        require '1.6.10'
      }
    }
    implementation('org.jetbrains.kotlin:kotlin-stdlib') {
      because 'version 1.3.5 as defined by nebula gradle-info-plugin has a known vulnerability'
      version {
        require '1.6.10'
      }
    }
    implementation('org.bouncycastle:bcpg-jdk15on') {
      because 'version 1.64 as defined by nebula gradle-info-plugin has a known vulnerability'
      version {
        require '1.70'
      }
    }
    implementation('org.eclipse.jgit:org.eclipse.jgit') {
      because 'version 1.64 as defined by nebula gradle-info-plugin has a known vulnerability'
      version {
        require '5.13.0.202109080827-r'
      }
    }
  }
}

dependencyCheck {
  failBuildOnCVSS=7
  suppressionFiles=["./cve-suppressions.xml"]
  skipConfigurations=["spotbugs"]
}

gradlePlugin {
  plugins {
    plugin {
      description = 'Gradle plugin extending the nebula-info-plugin providing extension points and support for other CI servers.'
      tags.set(['ci', 'info', 'build'])
      id = 'com.fizzpod.info'
      displayName = 'Gradle Extended Info Plugin'
      implementationClass = 'com.fizzpod.gradle.plugins.info.InfoPlugin'
    }
  }
}
